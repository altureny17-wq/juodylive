buildscript {
    ext.kotlin_version = '2.1.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.6.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven { url 'https://jitpack.io' }
        // هذا المستودع يحتوي على النسخ البديلة التي لم تُحذف
        maven { url "https://s01.oss.sonatype.org/content/repositories/releases/" }
    }
    
    configurations.all {
        resolutionStrategy {
            force "com.google.android.gms:play-services-location:21.0.1"
            
            // إلغاء أي إجبار قديم للإصدارات المحذوفة
            eachDependency { DependencyResolveDetails details ->
                if (details.requested.group == 'com.arthenica' && details.requested.name.contains('ffmpeg-kit')) {
                    details.useVersion '6.0.3-LTS' 
                }
            }
        }
    }
}


rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty('android')) {
            project.android {
                if (namespace == null) {
                    namespace project.group
                }
            }
        }
    }
}

subprojects {
    project.evaluationDependsOn(':app')
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
